
    == UW Astr 302, Winter 2026 ==

The FORTRAN code below includes three subroutines from the code Dusty:
SUBROUTINE INPUT
FUNCTION RDINP 
FUNCTION VAL

The last two are added for completeness (they are called from SUBROUTINE INPUT).

SUBROUTINE INPUT is the key piece of code for our Astr 302 project: it reads an
input file (e.g. sphere1.inp) and acquires all the input parameters needed by Dusty.
If the input file is not properly formated, it throws an error.

We want to write a python function that, for a given input file, will do the following:

1) if the provided file doesn't exist, gracefully throw an error

2) read all the input parameters; if no problems are encountered, for now simply
    return boolean True; eventually we will return an instance of a class (to be
    designed) that contains all the Dusty's input parameters

3) if any problem with reading the input file is encountered, gracefully catch the
    error and return boolean False

4) control verbosity with the default input verbose. If 0, no messages are printed.
   If 1, only a few messages about the progress are printed (so we'd know when it
   encountered an error. If verbose=2, print all the progress and if verbose=3,
   also print the values of all the input values as they are read in. 

For example, something like that: 

def IsDustyInputOK(filepath, verbose=0):

    # check first that input file exists
    # ...

    # now read all the input parameters
    # ...  catch errors! ... 

    # it read all the parameters w/o an error: 
    return True


[Z.I. Feb. 2026] 


                                                                                
c ***********************************************************************       
      SUBROUTINE INPUT(nameIn,nG,nameOut,nameQ,nameNK,TAU1,TAU2,TAUin,          
     &                 Nrec,GridType,Nmodel,error,version)                      
c =======================================================================       
c This subroutine reads input data from the file 'filename'. It                 
c utilizes the subroutine RDINP written by Moshe Elitzur.                       
c                                                      [Z.I., Nov. 1995]        
c =======================================================================       
      IMPLICIT none                                                             
      INTEGER npY, npP, npL, npG                                                
      INCLUDE 'userpar.inc'                                                     
      PARAMETER (npG=1)                                                         
      INTEGER denstyp, Ntr, iterETA, Eta7OK, nYEta7                             
      CHARACTER*235 nameETA                                                     
      LOGICAL RDW                                                               
      DOUBLE PRECISION pow, Yout, ETAcoef(npY,4), ptr(10), Ytr(10),             
     &       ETAdiscr(npY), yEta7(1000), Eta7(1000)                             
      COMMON /density1/ denstyp, Ntr, iterETA, nYEta7, Eta7OK, nameETA          
      COMMON /density2/ pow, Yout, ETAcoef, Ytr, ptr, ETAdiscr, Eta7,           
     &                  yEta7, RDW                                              
      INTEGER iLfid, szds, top, Nfiles                                          
      DOUBLE PRECISION TAUtot(npL), SigmaA(npG,npL), SigmaS(npG,npL),           
     &     omega(npL,npY), Tsub(npG), abund(npG,npY), TAUmax,                   
     &     xC(10), xCuser(10), SigExfid, TAUfid, lamfid, qsd,                   
     &     a1, a2, aveV                                                         
      COMMON /optprop/ TAUtot, SigmaA, SigmaS, omega, Tsub, abund,              
     &            TAUmax, xC, xCuser, SigExfid, TAUfid, lamfid, qsd,            
     &            a1, a2, aveV, iLfid, szds, top, Nfiles                        
      INTEGER Ncav, Nins                                                        
      DOUBLE PRECISION accRomb, accuracy, accConv, delTAUsc, facc,              
     &                 dynrange, EtaRat, accFbol                                
      COMMON /numerics/ accRomb, accuracy, accConv, delTAUsc, facc,             
     &                  dynrange, EtaRat, accFbol, Ncav, Nins                   
      CHARACTER*100 zline(999)                                                  
      INTEGER iINP, iSUM, iOUT, iVerb, iSPP, iA, iB, iC, iX, NlambdaOut,        
     &         iInn, iPsf, iV, Nconv, Nvisi                                     
      DOUBLE PRECISION LambdaOut(20), ConvInt(20,1000), Visib(20,1000),         
     &       Offset(1000), qtheta1(1000), Te_min                                
      COMMON /output/ LambdaOut, ConvInt, Visib, Offset, qtheta1,               
     &      Te_min, iPSF, NlambdaOut, iINP, iSUM, iOUT, iVerb, iSPP,            
     &      iA, iB, iC, iX, iInn, iV, Nconv, Nvisi, zline                       
      INTEGER startyp(2), Nlamtr(2), nBB(2)                                     
      CHARACTER nameStar(2)*235                                                 
      DOUBLE PRECISION Tstar, lamtr(2,101), klam(2,100), Tbb(2,10),             
     &                 rellum(2,10), mu1, ksi, mu2, xSiO, r1rs                  
      COMMON /source/ Tstar, lamtr, klam, Tbb, rellum, mu1, ksi, mu2,           
     &                xSiO, r1rs, startyp, Nlamtr, nBB, nameStar                
      INTEGER psftype, Npsf, iLambda                                            
      DOUBLE PRECISION kPSF(20), FWHM1(20), FWHM2(20), theta1,                  
     &       xpsf(1000), ypsf(1000)                                             
      COMMON /psf1/ iLambda, psftype, Npsf                                      
      COMMON /psf2/ kPSF, FWHM1, FWHM2, Theta1,                                 
     &       xpsf, ypsf                                                         
      CHARACTER LamStr(20)*72, strpow*72, strg*40, version*3                    
      CHARACTER*(*) nameIn, nameOut, nameQ(npG), nameNK(10), namePSF*70,        
     &         nameTAU*70                                                       
      INTEGER iG, nG, Nmodel, i, EtaOK, error, GridType, istop,                 
     &        ioverflw, Nrec, Nmax                                              
c     Nmax is the size of user supplied ETA file                                
      PARAMETER (Nmax = 1000)                                                   
      DOUBLE PRECISION RDINP, TAU1, TAU2, sum, a, b, x(Nmax), e(Nmax),          
     &       aa(Nmax), bb(Nmax), TAUin(Nrec), Ceta, x1, psf1                    
      LOGICAL Equal, NoEqual                                                    
      Equal = .True.                                                            
      NoEqual = .False.                                                         
c -----------------------------------------------------------------------       
      error = 0                                                                 
c     open output file                                                          
      open(12,file=nameOut,STATUS='UNKNOWN')                                    
      write(12,*)'==========================='                                  
      write(12,*)' Output from program Dusty '                                  
      write(12,*)' Version: ',version                                           
      write(12,*)'==========================='                                  
      write(12,*)' '                                                            
      write(12,*)' INPUT parameters from file:'                                 
      write(12,'(2x,a70)')nameIn                                                
      write(12,*)' '                                                            
c     open input file                                                           
      open(1,ERR=998,file=nameIn,STATUS='OLD')                                  
      rewind(1)                                                                 
                                                                                
c     *************************                                                 
c     ** PHYSICAL PARAMETERS **                                                 
c     *************************                                                 
                                                                                
c     1) EXTERNAL RADIATION                                                     
      CALL InpStar(error,1,nameIn)                                              
      IF(error.NE.0) goto 996                                                   
                                                                                
c     2) DUST PROPERTIES                                                        
c     # of different dust grains, to be used in a future version                
c     nG = RDINP(Equal,1)                                                       
      nG = 1                                                                    
c     2.1 Chemical Composition                                                  
c     type of optical properties                                                
      top = RDINP(Equal,1)                                                      
      IF (top.NE.1.AND.top.NE.2.AND.top.NE.3) THEN                              
        CALL MSG(9)                                                             
        error = 1                                                               
        GOTO 999                                                                
      END IF                                                                    
c     for top.LT.3 read in abundances for supported grains                      
      IF (top.LT.3) THEN                                                        
        xC(1) = RDINP(Equal,1)                                                  
        IF (xC(1).LT.0.0) xC(1) = 0.0                                           
        sum = xC(1)                                                             
        DO i = 2, 7                                                             
c         special care to be taken of graphite (1/3-2/3 rule):                  
          IF (i.NE.5) THEN                                                      
            xC(i) = RDINP(NoEqual,1)                                            
            IF (xC(i).LT.0.0) xC(i) = 0.0                                       
c           graphite (perpendicular to c axis) :                                
            IF (i.EQ.4) xC(i) = 2.*xC(i)/3.                                     
          ELSE                                                                  
c           graphite (parallel to c axis) :                                     
            xC(i) = 0.5 * xC(i-1)                                               
          END IF                                                                
          sum = sum + xC(i)                                                     
        END DO                                                                  
      END IF                                                                    
c     user supplied n and k:                                                    
      IF (top.EQ.2) THEN                                                        
        Nfiles = RDINP(Equal,1)                                                 
c       file names                                                              
        strg = 'optical constants:'                                             
        DO i = 1, Nfiles                                                        
          CALL FileMSG(nameNK(i),strg)                                          
        END DO                                                                  
        IF(error.NE.0) goto 996                                                 
c       abundances                                                              
        xCuser(1) = RDINP(Equal,1)                                              
        IF (xCuser(1).LT.0.0) xCuser(1) = 0.0                                   
        sum = sum + xCuser(1)                                                   
        IF (Nfiles.GT.1) THEN                                                   
          DO i = 2, Nfiles                                                      
            xCuser(i) = RDINP(NoEqual,1)                                        
            IF (xCuser(i).LT.0.0) xCuser(i) = 0.0                               
            sum = sum + xCuser(i)                                               
          END DO                                                                
        END IF                                                                  
      END IF                                                                    
      IF (top.LT.3) THEN                                                        
        IF (sum.LE.0.0) THEN                                                    
          CALL MSG(5)                                                           
          error = 1                                                             
          GOTO 999                                                              
        END IF                                                                  
c       normalize abundances for supported grains:                              
        DO i = 1, 7                                                             
          xC(i) = xC(i) / sum                                                   
        END DO                                                                  
c       normalize abundances for user supplied grains                           
        IF (top.EQ.2) THEN                                                      
          DO i = 1, Nfiles                                                      
            xCuser(i) = xCuser(i) / sum                                         
          END DO                                                                
        END IF                                                                  
      END IF                                                                    
c     user supplied cross-sections:                                             
      IF (top.EQ.3) THEN                                                        
c       filename for Qabs and Qsca                                              
        strg= 'abs. and scatt. cross-sections:'                                 
        DO iG = 1, nG                                                           
          CALL FileMSG(nameQ(iG),strg)                                          
        END DO                                                                  
      END IF                                                                    
                                                                                
c     2.2 Grain size distribution                                               
      IF (top.NE.3) THEN                                                        
c       type of size distribution                                               
        szds = RDINP(Equal,1)                                                   
        IF (szds.NE.1.AND.szds.NE.2.AND.szds.NE.3) THEN                         
          CALL MSG(10)                                                          
          error = 1                                                             
          GOTO 999                                                              
        END IF                                                                  
c       grain sizes                                                             
        IF (szds.GT.1) THEN                                                     
          qsd = RDINP(Equal,1)                                                  
          a1 = RDINP(Equal,1)                                                   
          IF (a1.LE.0.0) a1 = 0.0001                                            
          a2 = RDINP(Equal,1)                                                   
          IF (szds.EQ.2.AND.a2.LT.a1) a2 = a1                                   
        ELSE                                                                    
          qsd = 3.5                                                             
          a1 = 0.005                                                            
          a2 = 0.25                                                             
        END IF                                                                  
      END IF                                                                    
                                                                                
c     2.3 Dust temperature on inner boundary                                    
      DO iG = 1, nG                                                             
        Tsub(iG) = RDINP(Equal,1)                                               
      END DO                                                                    
                                                                                
c     3) DENSITY DISTRIBUTION                                                   
                                                                                
c     parameter describing eta function:                                        
      denstyp = RDINP(Equal,1)                                                  
c     WriteOut prints all input data, read so far, in fname.out 	               
c     It has to be placed here b/c geometry (spherical or slab) is not determine
c     until denstyp is read [MN,Sep'99].                                        
      CALL WriteOut(1,nG,nameQ,nameNK)                                          
c     The following transfers are caused by the change of order                 
c     of density types [Dec'96]                                                 
      IF (denstyp.EQ.1) THEN                                                    
        denstyp = 2                                                             
        ELSE                                                                    
        IF (denstyp.EQ.2) THEN                                                  
          denstyp = 3                                                           
          ELSE                                                                  
          IF (denstyp.EQ.3) THEN                                                
            denstyp = 5                                                         
            ELSE                                                                
            IF (denstyp.EQ.4) THEN                                              
              denstyp = 4                                                       
              ELSE                                                              
              IF (denstyp.EQ.5) denstyp = 7                                     
            END IF                                                              
          END IF                                                                
        END IF                                                                  
      END IF                                                                    
c     initialize the logical variable RDW (in 'denstyp.inc', used in ChkSplin)  
      IF(denstyp.eq.5.OR.denstyp.eq.6) THEN                                     
        RDW = .true.                                                            
      ELSE                                                                      
        RDW = .false.		  	                                                      
      END IF                                                                    
                                                                                
      EtaOK = 0                                                                 
      Ntr = 0                                                                   
c     Read parameters for each type of density distribution                     
c     slab geometry                                                             
      IF (denstyp.EQ.0) THEN                                                    
        EtaOK = 1                                                               
        write(12,'(a33)') ' Calculation in planar geometry:'                    
        mu1 = RDINP(Equal,1)                                                    
        IF (mu1.GT.1.0) mu1 = 1.0                                               
        IF (mu1.LT.0.0) mu1 = -1.0                                              
        write(12,'(a35,1p,e11.3)')                                              
     &                ' cos of left illumination angle = ',mu1                  
        ksi = RDINP(Equal,1)                                                    
        IF (ksi.LT.0.0) ksi = 0.0                                               
        IF (ksi.GT.1.0) THEN                                                    
          ksi = 1.0                                                             
        END IF                                                                  
        write(12,'(a6,1p,e11.3)')'  R = ',ksi                                   
        IF(ksi.GT.0.) THEN                                                      
c         in case of additional source on the right                             
          mu2 = RDINP(Equal,1)                                                  
          IF (mu2.GT.1.0) mu2 = 1.0                                             
          IF (mu2.LT.0.0) mu2 = -1.0                                            
          write(12,'(a36,1p,e10.3)')                                            
     &                ' cos of right illumination angle = ',mu2                 
          CALL InpStar(error,2,nameIn)                                          
          CALL WriteOut(2,nG,nameQ,nameNK)                                      
        ELSE                                                                    
c         even if no second source is supplied, mu2 needs a value               
c         of 1 to avoid crashing in the formulae in SLBStar [MN]                
          mu2 = 1.                                                              
        END IF                                                                  
      ELSE                                                                      
        ksi = 0.                                                                
      END IF                                                                    
c     smooth or broken power laws                                               
      IF (denstyp.EQ.1.OR.denstyp.EQ.2) THEN                                    
        EtaOK = 1                                                               
        Ntr = RDINP(Equal,1)                                                    
c       changed definition                                                      
        Ntr = Ntr - 1                                                           
c       read in transition radii                                                
        IF (Ntr.GT.0) THEN                                                      
          Ytr(1) = RDINP(Equal,1)                                               
          IF (Ntr.GT.1) THEN                                                    
            DO i = 2, Ntr                                                       
              Ytr(i) = RDINP(NoEqual,1)                                         
            END DO                                                              
          END IF                                                                
          Yout = RDINP(NoEqual,1)                                               
        ELSE                                                                    
          Yout = RDINP(Equal,1)                                                 
        END IF                                                                  
        IF (Yout.LE.1.0) Yout = 1.001                                           
c       read in powers                                                          
        pow = RDINP(Equal,1)                                                    
        IF (Ntr.GT.0) THEN                                                      
          DO i = 1, Ntr                                                         
            ptr(i) = RDINP(NoEqual,1)                                           
          END DO                                                                
        END IF                                                                  
c       print info to the output file                                           
        IF (Ntr.EQ.0) THEN                                                      
          CALL getfs(pow,2,0,strpow)                                            
          write(12,'(a38,a5)')                                                  
     &      ' Density described by 1/r**k with k =',strpow                      
          write(12,'(a21,1p,e10.3)')'  Relative thickness:',Yout                
        ELSE                                                                    
          IF (denstyp.EQ.1) THEN                                                
            write(12,*)' Density described by smooth power law'                 
          ELSE                                                                  
            write(12,*)' Density described by a broken power law:'              
          END IF                                                                
          write(12,*)'  power   Ytransition'                                    
          write(12,*)'  -------------------'                                    
          write(12,*)'              1.0'                                        
          CALL getfs(pow,2,0,strpow)                                            
          write(12,'(a2,a5)')'  ',strpow                                        
          DO i = 1, Ntr                                                         
            write(12,'(a10,1p,e10.3)')'          ',Ytr(i)                       
            CALL getfs(ptr(i),2,0,strpow)                                       
            write(12,'(a2,a5)')'  ',strpow                                      
          END DO                                                                
          write(12,'(a10,1p,e10.3)')'          ',Yout                           
        END IF                                                                  
      END IF                                                                    
c     exponential law                                                           
      IF (denstyp.EQ.3) THEN                                                    
        EtaOK = 1                                                               
        Yout = RDINP(Equal,1)                                                   
        IF (Yout.LE.1.0) Yout = 1.001                                           
        pow = RDINP(Equal,1)                                                    
        IF (pow.LE.0.0) THEN                                                    
          EtaOK = 0                                                             
          ELSE                                                                  
          write(12,*)' Density described by exponential distribution'           
          write(12,'(a21,1p,e10.3)')'               Sigma:',pow                 
          write(12,'(a21,1p,e10.3)')'  Relative thickness:',Yout                
        END IF                                                                  
      END IF                                                                    
c     default approximation and default numerics for rad. driven winds          
      IF (denstyp.EQ.4.OR.denstyp.EQ.5) THEN                                    
        EtaOK = 1                                                               
        Yout = RDINP(Equal,1)                                                   
        IF (Yout.LE.1.0) Yout = 1.001                                           
c       ** DEFAULT ** for epsilon = v1/ve = u1/ue:                              
        pow = 0.2                                                               
        IF(denstyp.EQ.5) THEN                                                   
c         ** DEFAULT ** for Max(GravCor = Fgrav/Frad_press):                    
          ptr(1) = 0.5                                                          
c         convergence criterion:                                                
          ptr(2) = 1.0                                                          
        END IF                                                                  
        write(12,*)' Density for radiatively driven winds from'                 
        IF (denstyp.EQ.4) THEN                                                  
          write(12,*)' analytic approximation for gray dust.'                   
        ELSE                                                                    
          write(12,*)' full dynamic calculation.'                               
        END IF                                                                  
        write(12,'(a21,1p,e10.3)')'  Relative thickness:',Yout                  
      END IF                                                                    
c     full dynamical calculation for radiatively driven winds                   
      IF (denstyp.EQ.6) THEN                                                    
        EtaOK = 1                                                               
c       Yout:                                                                   
        Yout = RDINP(Equal,1)                                                   
        IF (Yout.LE.1.0) Yout = 1.001                                           
c       u1:                                                                     
        pow = RDINP(Equal,1)                                                    
c       GravCor:                                                                
        ptr(1) = RDINP(Equal,1)                                                 
c       convergence criterion:                                                  
        ptr(2) = 1.0                                                            
c       default limits                                                          
        IF (ptr(1).GT.1.0) ptr(1) = 0.9999                                      
        IF (ptr(1).LT.0.0) ptr(1) = 0.0                                         
        write(12,*)' Density for radiatively driven winds:'                     
        write(12,*)' Full dynamic calculation (denstyp.EQ.6),'                  
        write(12,'(a6,1p,e10.3,a20,e10.3)')                                     
     &                   '  u1 =',pow,' Gamma^{-1}_{max} =',ptr(1)              
      END IF                                                                    
c     user specified table for ETA                                              
      IF(denstyp.EQ.7) THEN                                                     
        EtaOK = 1                                                               
        strg = 'dust density distribution:'                                     
        CALL FileMSG(nameETA,strg)                                              
        write(12,*)' Density distribution supplied from file:'                  
        write(12,'(2x,a70)') nameETA                                            
c       read in the density                                                     
        open(31,ERR=997,file=nameETA,STATUS='OLD')                              
c       three lines in the header:                                              
        DO i = 1, 3                                                             
          read(31,*,ERR=997)                                                    
        END DO                                                                  
        istop = 0                                                               
        i = 0                                                                   
        DO WHILE (istop.GE.0)                                                   
          read(31,*,END=900,ERR=997,iostat=istop)a, b                           
          IF (istop.GE.0) THEN                                                  
            i = i + 1                                                           
            x(i) = a                                                            
            e(i) = b                                                            
            IF (i.EQ.1) x1 = x(i)                                               
            yEta7(i) = x(i) / x1                                                
          END IF                                                                
        END DO                                                                  
900     close(31)                                                               
        Eta7OK = 7                                                              
        nYEta7 = i                                                              
        IF (nYEta7.LT.2) goto 997                                               
c       if input positions in descending order turn them around                 
        IF (yEta7(1).GT.yEta7(2)) THEN                                          
          DO i = 1, nYEta7                                                      
            aa(i) = yEta7(i)                                                    
            bb(i) = e(i)                                                        
          END DO                                                                
          DO i = 1, nYEta7                                                      
            yEta7(i) = aa(nYEta7+1-i)                                           
            e(i) = bb(nYEta7+1-i)                                               
          END DO                                                                
        END IF                                                                  
c       relative thickness                                                      
        Yout = yEta7(nYEta7)                                                    
        write(12,'(a21,1p,e10.3)')'  Relative thickness:',Yout                  
        IF (Yout.LE.1.0) Yout = 1.001                                           
c       integrate and ...                                                       
        CALL SIMPSON(Nmax,1,nYEta7,yEta7,e,Ceta)                                
c       ... renormalize                                                         
        DO i = 1, nYEta7                                                        
          Eta7(i) = e(i) / Ceta                                                 
        END DO                                                                  
      END IF                                                                    
c     done with the reading of density distribution                             
      IF (EtaOK.NE.1) THEN                                                      
        CALL MSG(3)                                                             
        error = 1                                                               
        GOTO 999                                                                
      END IF                                                                    
      write(12,*)' --------------------------------------------'                
                                                                                
c     4) OPTICAL DEPTH                                                          
c     grid type                                                                 
      GridType = RDINP(Equal,1)                                                 
      IF (GridType.EQ.3) THEN                                                   
c     TAU-grid from a file                                                      
        strg = 'user supplied TAU-grid:'                                        
        CALL FileMSG(nameTAU,strg)                                              
c       read optical depths                                                     
        open(32,ERR=992,file=nameTAU,STATUS='OLD')                              
c       fiducial wavelength                                                     
c       (the second argument of RDINP is the unit)                              
        lamfid = RDINP(Equal,32)                                                
        i = 0                                                                   
        DO WHILE (i.LT.Nrec)                                                    
          read(32,*,END=902,ERR=992) a                                          
          i = i + 1                                                             
          TAUin(i) = a                                                          
        END DO                                                                  
902     close(32)                                                               
        Nmodel = i                                                              
c       sort the tau-grid                                                       
        CALL Sort(TAUin,Nmodel)                                                 
        TAU1 = TAUin(1)                                                         
        IF (TAU1.LE.0.0) TAU1 = 0.0001                                          
        TAU2 = TAUin(Nmodel)                                                    
      ELSE                                                                      
c      fiducial wavelength                                                      
       lamfid = RDINP(Equal,1)                                                  
c      total optical depths at lamfid                                           
       TAU1 = RDINP(Equal,1)                                                    
       IF (TAU1.LE.0.0) TAU1 = 0.0001                                           
       TAU2 = RDINP(Equal,1)                                                    
       IF (TAU2.LE.TAU1) THEN                                                   
         TAU2 = TAU1                                                            
         Nmodel = 1                                                             
       END IF                                                                   
c      read number of models                                                    
       Nmodel = RDINP(Equal,1)                                                  
       IF (Nmodel.GT.(Nrec-1)) Nmodel = Nrec-1                                  
       IF (Nmodel.LT.1) Nmodel = 1                                              
      END IF                                                                    
      IF (Nmodel.GT.1) THEN                                                     
        write(12,'(a19,1p,e8.1,a8)')' Optical depths at',lamfid,                
     &                              'microns'                                   
        write(12,'(a14,1p,e9.2,a3,e9.2)')' ranging from',TAU1,' to',TAU2        
        IF (GridType.EQ.1) strg=' models with linear grid    '                  
        IF (GridType.EQ.2) strg=' models with logarithmic grid'                 
        IF (GridType.EQ.3) strg=' models with grid from file  '                 
        write(12,'(a1,i4,a)')' ', Nmodel, strg                                  
        IF (GridType.EQ.3) write(12,'(a4,a70)')'    ',nameTAU                   
      ELSE                                                                      
        write(12,'(a18,1p,e8.1,a9,e9.2)')' Optical depth at',lamfid,            
     &                                  ' microns:',TAU1                        
      END IF                                                                    
                                                                                
c     ************************                                                  
c     ** NUMERICAL ACCURACY **                                                  
c     ************************                                                  
c     some of the parameters were originally left to user to                    
c     specify them, most of them are now unchangeable defaults                  
c     max. increase of scaled TAU/TAUtot                                        
c      delTAUsc = RDINP(Equal,1)                                                
c      IF (delTAUsc.LE.0.0) delTAUsc = 0.3                                      
      delTAUsc = 0.3                                                            
c     max. increase in the ratio of two y                                       
c      facc = RDINP(Equal,1)                                                    
c      IF (facc.LE.0.0) facc = 2.0                                              
      facc = 2.0                                                                
c     max allowed ratio of Eta for two consecutive pts.                         
      EtaRat = 4.0                                                              
c     # of rays per radial step                                                 
c      Nins = RDINP(Equal,1)                                                    
c      IF (Nins.LE.0) Nins = 2                                                  
      Nins = 2                                                                  
c     accuracy for numerical integration in ROMBERG                             
c      accRomb = RDINP(Equal,1)                                                 
c      IF (accRomb.LE.0.0) accRomb = 0.0001                                     
c      IF (accRomb.GT.0.005) accRomb = 0.005                                    
      accRomb = 0.0001                                                          
c     accuracy for flux conservation                                            
      accuracy = RDINP(Equal,1)                                                 
      IF (accuracy.LE.0.0) accuracy = 0.05                                      
c     protect against a very large value for accuracies                         
      IF (accuracy.GT.0.25) accuracy = 0.25                                     
c     accuracy for convergence (typical 0.0001)                                 
      accConv = accuracy / 500.0                                                
      accFbol = 10.*accConv                                                     
c     dynamical range                                                           
c      dynrange = RDINP(Equal,1)                                                
c      IF (dynrange.LE.0.0) dynrange = 1.0e-14                                  
c      IF (dynrange.GT.0.0001) dynrange = 1.0e-4                                
      dynrange = 1.0e-15                                                        
      IF (accuracy.GE.0.1) THEN                                                 
        CALL getfs(accuracy*100,0,1,strpow)                                     
        write(12,'(a20,a3,a1)')' Required accuracy:',strpow,'%'                 
      ELSE                                                                      
        CALL getfs(accuracy*100,0,1,strpow)                                     
        write(12,'(a20,a2,a1)')' Required accuracy:',strpow,'%'                 
      END IF                                                                    
      write(12,*)' --------------------------------------------'                
c     ******************                                                        
c     ** OUTPUT FLAGS **                                                        
c     ******************                                                        
c     A flag for additional output for us only [MN]:                            
c     If iInn=1: print err.vs.iter in unt=38 (fname.err) for all models         
c     and additionally list scaled fbol(y) and Ubol(y) in m-files.              
c     iInn replaces iLTS in 'output.inc'                                        
      iInn = 0                                                                  
c     these two by default                                                      
      iSUM = 1                                                                  
      iOUT = 1                                                                  
      iVerb = RDINP(Equal,1)                                                    
c     spectral properties                                                       
      iSPP = RDINP(Equal,1)                                                     
c     spectra                                                                   
      iA = RDINP(Equal,1)                                                       
c     images (intensity) for spherical only                                     
      IF(denstyp.NE.0) THEN                                                     
       iC = RDINP(Equal,1)                                                      
       IF (iC.NE.0) THEN                                                        
        NlambdaOut = RDINP(Equal,1)                                             
        IF (NlambdaOut.GE.1) THEN                                               
          DO i = 1, NlambdaOut                                                  
            LambdaOut(i) = RDINP(NoEqual,1)                                     
c           make sure the wavelengths are inside Dusty's range                  
            IF (LambdaOut(i).LE.0.01) LambdaOut(i) = 0.01                       
            IF (LambdaOut(i).GT.36000) LambdaOut(i) = 36000                     
          END DO                                                                
          ioverflw = 0                                                          
          DO i = 1, NlambdaOut                                                  
            IF (LambdaOut(i).LT.0.995) THEN                                     
              CALL getfs(LambdaOut(i),2,1,LamStr(i))                            
            ELSE                                                                
              IF (LambdaOut(i).LT.9.95) THEN                                    
               CALL getfs(LambdaOut(i),1,0,LamStr(i))                           
              ELSE                                                              
                IF (LambdaOut(i).LT.99.5) THEN                                  
                 CALL getfs(LambdaOut(i),0,0,LamStr(i))                         
                ELSE                                                            
                 CALL getfs(LambdaOut(i),0,1,LamStr(i))                         
c                  IDINT truncates the numbers                                  
c                  prn = IDINT(LambdaOut(i))                                    
c                  write (LamStr(i),'(I6)') prn                                 
                END IF                                                          
              END IF                                                            
            END IF                                                              
            IF (LambdaOut(i).GT.9999.5) THEN                                    
              ioverflw = 1                                                      
              strpow = LamStr(i)                                                
              strpow(4:4) = '*'                                                 
              strpow(5:5) = ' '                                                 
              LamStr(i) = strpow                                                
            END IF                                                              
          END DO                                                                
        END IF                                                                  
        write(12,*)' Images requested for these wavelengths (mic)'              
        write(12,'(a1,20a5)')' ',(LamStr(i),i=1,NlambdaOut)                     
        IF (ioverflw.EQ.1) write(12,*)'  *: in mm'                              
c       convolved images  (only for our use)                                    
        IF (iC.LT.0) THEN                                                       
         iPSF = RDINP(Equal,1)                                                  
         IF (iPSF.NE.0) THEN                                                    
           Theta1 = RDINP(Equal,1)                                              
           write(12,'(a39,1p,e7.1)')                                            
     &           ' Convolved images produced for theta1=',Theta1                
           psftype = RDINP(Equal,1)                                             
           IF (psftype.NE.1.AND.psftype.NE.2.AND.psftype.NE.3) goto 994         
           IF (psftype.LT.3) THEN                                               
c            Gaussians, read in parameters                                      
c            FWHM for the first component                                       
             FWHM1(1) = RDINP(Equal,1)                                          
             IF (NlambdaOut.GT.1) THEN                                          
               DO i = 2, NlambdaOut                                             
                FWHM1(i) = RDINP(NoEqual,1)                                     
               END DO                                                           
             END IF                                                             
             IF (psftype.EQ.2) THEN                                             
c             relative strength for the second component                        
              kPSF(1) = RDINP(Equal,1)                                          
              IF (NlambdaOut.GT.1) THEN                                         
                DO i = 2, NlambdaOut                                            
                  kPSF(i) = RDINP(NoEqual,1)                                    
                END DO                                                          
              END IF                                                            
c             FWHM for the second component                                     
              FWHM2(1) = RDINP(Equal,1)                                         
              IF (NlambdaOut.GT.1) THEN                                         
                DO i = 2, NlambdaOut                                            
                  FWHM2(i) = RDINP(NoEqual,1)                                   
                END DO                                                          
              END IF                                                            
             END IF                                                             
             write(12,*)' The point spread functions are Gaussians'             
           ELSE                                                                 
c           user supplied PSF                                                   
            strg = 'point spread function:'                                     
            CALL FileMSG(namePSF,strg)                                          
            write(12,*)' The point spread function supplied from file'          
            write(12,'(2x,a70)')namePSF                                         
            open(31,ERR=995,file=namePSF,STATUS='OLD')                          
c           three lines in the header:                                          
            DO i = 1, 3                                                         
              read(31,*,ERR=995)                                                
            END DO                                                              
            istop = 0                                                           
            i = 0                                                               
            DO WHILE (istop.GE.0)                                               
              read(31,*,END=901,ERR=995,iostat=istop)a, b                       
              IF (istop.GE.0) THEN                                              
                i = i + 1                                                       
                IF (i.EQ.1) THEN                                                
                  psf1 = b                                                      
                  IF (a.NE.0.0) goto 995                                        
                END IF                                                          
                xpsf(i) = a                                                     
                ypsf(i) = b / psf1                                              
              END IF                                                            
            END DO                                                              
901         close(31)                                                           
            Npsf = i                                                            
c           scale to 1 at the center                                            
            CALL ScaleTo1(1000,Npsf,ypsf)                                       
c           find equivalent FWHM                                                
            istop = 0                                                           
            i = 1                                                               
            DO WHILE (istop.EQ.0)                                               
              i = i + 1                                                         
              IF (ypsf(i).LE.0.5) istop = 1                                     
            END DO                                                              
c           linear interpolation                                                
            FWHM1(1) = (xpsf(i)-xpsf(i-1))/(ypsf(i)-ypsf(i-1))                  
            FWHM1(1) = (FWHM1(1)*(0.5-ypsf(i-1))+xpsf(i-1))*2                   
            FWHM2(1) = 0.0                                                      
            write(12,'(a18,1p,e8.1)')' Equivalent FWHM:',FWHM1(1)               
           END IF                                                               
         END IF                                                                 
        END IF                                                                  
c       visibility (only if the intensity is requested)                         
        iV = RDINP(Equal,1)                                                     
        IF(iV.NE.0) iV = iC                                                     
        write(12,*)' --------------------------------------------'              
       ELSE                                                                     
        iPSF = 0                                                                
        iV = 0                                                                  
       END IF                                                                   
      ELSE                                                                      
       iC = 0                                                                   
      END IF                                                                    
      write(12,*)' '                                                            
c     radial quantities                                                         
      iB = RDINP(Equal,1)                                                       
c     run-time messages                                                         
      iX = RDINP(Equal,1)                                                       
      iINP = 0                                                                  
c     *** DONE ***                                                              
c     if everything is OK, close the input file and finish                      
999   goto 996                                                                  
c     or in the case of err reading files...                                    
992   write(12,*)' ***  FATAL ERROR IN DUSTY  *************'                    
      write(12,*)' File with user supplied TAU-grid:'                           
      write(12,'(2x,a70)') nameTAU                                              
      write(12,*)' is not properly formatted?!'                                 
      write(12,*)' ****************************************'                    
      close(12)                                                                 
      error = 3                                                                 
      goto 996                                                                  
994   CALL MSG(12)                                                              
      close(12)                                                                 
      error = 3                                                                 
      goto 996                                                                  
995   write(12,*)' ***  FATAL ERROR IN DUSTY  *************'                    
      write(12,*)' File with the point spread function:'                        
      write(12,'(a2,a70)')'  ', namePSF                                         
      write(12,*)' is not properly formatted?!'                                 
      write(12,*)' ****************************************'                    
      close(12)                                                                 
      error = 3                                                                 
997   write(12,*)' ***  FATAL ERROR IN DUSTY  *************'                    
      write(12,*)' File with the dust density distribution:'                    
      write(12,'(2x,a70)') nameETA                                              
      write(12,*)' is not properly formatted?!'                                 
      write(12,*)' ****************************************'                    
      close(12)                                                                 
      error = 3                                                                 
998   write(12,*)' ***  FATAL ERROR IN DUSTY  ****'                             
      write(12,*)' Input file:'                                                 
      write(12,'(2x,a70)') nameIn                                               
      write(12,*)' is missing?!'                                                
      write(12,*)' *******************************'                             
      close(12)                                                                 
      error = 3                                                                 
c -----------------------------------------------------------------------       
996   close(1)                                                                  
      RETURN                                                                    
      END                                                                       
c ***********************************************************************       



                                                                            
                                                                                
c ***********************************************************************       
c This function is taken from Moshe Elitzur.           [Z.I., Nov. 1995]        
c =======================================================================       
      DOUBLE PRECISION FUNCTION RDINP(Equal,IUNIT)                              
c =======================================================================       
C     Read lines, up to 232 long, from pre-opened unit IUNIT and extract        
C     all input numbers from them. When EQUAL is set, numeric input data        
C     must be preceded by an equal sign.All non-numeric data and numbers        
C     not preceded by = when EQUAL is on are ignored.RDINP = next number        
C     encountered (after equal sign) and terminated by a nun-numeric            
C     symbol (termination with blank is best). Commas and exponential           
C     notation are allowed.  All text after % is ignored, as in TeX.            
C     Lines with * in the first column are echoed to the output device.         
C     The number is comprised of an actual VALUE, decimal part FRAC and         
C     (integer) exponent PWR.  It has a SIGN, and the exponential power         
C     has sign SIGNEX. Logical flag to the decimal part is set in               
C     DECIMAL. The search is conducted between FIRST, which is                  
C     continuously increased, and LAST.  A new line is read when FIRST          
C     exceeds LAST, and can be forced by calling with -IUNIT.  Actual           
C     extraction of numerical value is done in separate FUNCTION VAL.           
c =======================================================================       
      IMPLICIT None                                                             
      Integer IUnit,Ind,First, Last                                             
      DOUBLE PRECISION Value,Val,Frac,Pwr,Sign,Signex                           
      CHARACTER Card*(232),CR,prev,Term,Next                                    
      Logical Equal,digit,minus,sgn,dot,E,decimal                               
      Save Card,First,Last                                                      
      DATA First/1/, Last/0/                                                    
c -----------------------------------------------------------------------       
C FUNCTION STATEMENTS                                                           
      digit(CR) = CR.GE.'0' .AND. CR.LE.'9'                                     
      minus(CR) = CR.EQ.'-'                                                     
      sgn(CR)   = CR.EQ.'+' .OR. CR.EQ.'-'                                      
      dot(CR)   = CR.EQ.'.'                                                     
      E(CR)     = CR.EQ.'E' .OR. CR.EQ.'e'                                      
C                                                                               
      IF (IUnit.lt.0) Then                                                      
         First = Last + 1                                                       
         IUnit = -IUnit                                                         
      END IF                                                                    
c     Start the search for the next number:                                     
  1   RDINP  = 0.                                                               
      VALUE  = 0.                                                               
      FRAC   = 0.                                                               
      PWR    = 0.                                                               
      SIGN   = 1.                                                               
      SIGNEX = 1.                                                               
      Decimal = .False.                                                         
      If (first.gt.last) then                                                   
c     Time to get a new line                                                    
         READ (IUNIT, '(A)' , END = 99) Card                                    
         first = 1                                                              
         last = len(Card)                                                       
c        Find start of trailing junk:                                           
         DO WHILE (Card(last:last).LE.' ')                                      
          last = last - 1                                                       
          if (last.lt.first) goto 1                                             
         END DO	                                                                
         IF (Card(first:first).EQ.'*') WRITE (12,'(A)') Card(1:last)            
         ind = Index(Card,'%')                                                  
         if (ind.gt.0) last = ind - 1                                           
      End If                                                                    
                                                                                
c     Get past the next '=' when the EQUAL flag is set                          
      If (Equal) then                                                           
        DO WHILE (Card(first:first).NE.'=')                                     
          first = first + 1                                                     
          IF (first.GT.last) goto 1                                             
        END DO                                                                  
      End If                                                                    
c     OK, start searching for the next digit:                                   
      Do While (.not.digit(Card(first:first)))                       		         
	    first = first + 1                                                          
		if (first.gt.last) goto 1                                                     
	End Do                                                                         
c     Check if it is a negative or decimal number                               
      If (first.gt.1) then                                                      
         prev = Card(first-1:first-1)                                           
         if (minus(prev)) sign = -1.                                            
         if (dot(prev)) then                                                    
           decimal = .True.                                                     
           if (first.gt.2 .and. minus(Card(first-2:first-2))) sign = -1.        
         end if                                                                 
      End If                                                                    
c     Extract the numerical value                                               
      IF (.not.Decimal) Then                                                    
         Value = VAL(Card,first,last,decimal,Term)                              
c        Check for a possible decimal part.  Termination with '.E' or           
c        '.e' is equivalent to the same ending without '.'                      
         if (first.lt.last.and.dot(Term)) then                                  
            first = first + 1                                                   
            next = Card(first:first)                                            
            if (digit(next)) decimal = .true.                                   
            if (E(next)) Term = 'E'                                             
         end if                                                                 
      END IF                                                                    
c     Extract the decimal fraction, when it exists                              
      IF (Decimal) Frac = VAL(Card,first,last,decimal,Term)                     
c     An exponential may exist if any part terminated with 'E' or 'e'           
      IF (first.lt.last.and.E(term)) then                                       
         first = first + 1                                                      
         next = Card(first:first)                                               
         if (first.lt.last.and.sgn(next))then                                   
            first = first + 1                                                   
            if (minus(next)) Signex = -1.                                       
         end if                                                                 
         decimal = .False.                                                      
         PWR = VAL(Card,first,last,decimal,Term)                                
      END IF                                                                    
c     Finally, put the number together                                          
      RDINP = Sign*(Value + Frac)*10**(Signex*PWR)                              
      Return                                                                    
                                                                                
99    WRITE (12,'(3(1x,a,/))')                                                  
     *' ****TERMINATED. EOF reached by RDINP while looking for input. ',        
     *' *** Last line read:',Card                                               
c -----------------------------------------------------------------------       
      RETURN                                                                    
      END                                                                       
c ***********************************************************************       
                                                                                
                                                                                
                                                                                
c ***********************************************************************       
c This function is taken from Moshe Elitzur            [Z.I., Nov. 1995]        
c =======================================================================       
      DOUBLE PRECISION FUNCTION VAL(Card,first,last,decimal,Term)               
c     Extract numerical value from CARD, begining at position FIRST up          
c     to the first non-digit encountered.  The terminating character is         
c     returned in TERM, its position in FIRST. Commas are treated as            
c     simple separators.                                                        
c =======================================================================       
      IMPLICIT None                                                             
      Character Card*(*), Term, CH                                              
      Logical Decimal, digit                                                    
      Integer IVAL, first, last, first0                                         
      Real pwr                                                                  
c -----------------------------------------------------------------------       
C     FUNCTION STATEMENTS                                                       
      IVAL(CH) = ICHAR(CH) - ICHAR('0')                                         
      digit(CH) = CH.GE.'0' .AND. CH.LE.'9'                                     
c                                                                               
      VAL = 0.                                                                  
      pwr = 1.                                                                  
      first0 = first                                                            
      DO 10 first = first0, last                                                
         Term = Card(first:first)                                               
         if (Term.eq.',') goto 10                                               
         if (.not.digit(Term)) Return                                           
         If (decimal) then                                                      
            pwr = pwr*0.1                                                       
            Val = Val + pwr*IVAL(Term)                                          
         Else                                                                   
            Val = 10.*Val + IVAL(Term)                                          
         End If                                                                 
  10  CONTINUE                                                                  
      Term = ' '                                                                
c -----------------------------------------------------------------------       
      RETURN                                                                    
      END                                                                       
c ***********************************************************************



